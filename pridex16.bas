01 REM @COMRADE_CATSCRATCH@MASTODON.SOCIAL
02 REM #16BITPROGRESS
03 REM INSPIRED BY GITHUB.COM/NCOMMANDER/PROGRESS-PRIDE-WIN16
04 REM AN EXCERPT FROM README.MD
05 REM TO REMEMBER THOSE LOST AT CLUB Q IN COLORADO SPRINGS ON NOV 19TH 2022
06 REM AND ALL OTHERS WHO WERE LOST TO GUN VIOLENCE THE 611 SHOOTINGS THIS YEAR
10 SCREEN$80
11 COLOR 0,0
30 REM BLACK 0
31 VPOKE 1,$FA00,$00
32 VPOKE 1,$FA01,$00
40 REM RED 1
41 VPOKE 1,$FA02,$00
42 VPOKE 1,$FA03,$0E
50 REM ORANGE 2
51 VPOKE 1,$FA04,$80
52 VPOKE 1,$FA05,$0F
60 REM YELLOW 3
61 VPOKE 1,$FA06,$E0
62 VPOKE 1,$FA07,$0F
70 REM GREEN 4
71 VPOKE 1,$FA08,$82
72 VPOKE 1,$FA09,$00
80 REM BLUE 5
81 VPOKE 1,$FA0A,$4F
82 VPOKE 1,$FA0B,$00
90 REM PURPLE 6
91 VPOKE 1,$FA0C,$08
92 VPOKE 1,$FA0D,$07
110 REM BROWN 7
111 VPOKE 1,$FA0E,$31
112 VPOKE 1,$FA0F,$06
120 REM SKY BLUE 8
121 VPOKE 1,$FA10,$DE
122 VPOKE 1,$FA11,$07
130 REM PINK 9
131 VPOKE 1,$FA12,$AC
132 VPOKE 1,$FA13,$0F
140 REM WHITE 10
141 VPOKE 1,$FA14,$FF
142 VPOKE 1,$FA15,$0F
150 REM INTERYELLOW 11
151 VPOKE 1,$FA16,$D0
152 VPOKE 1,$FA17,$0F
160 REM INTERPURPLE 12
161 VPOKE 1,$FA18,$0A
162 VPOKE 1,$FA19,$07
170 REM GRAY 13
171 VPOKE 1,$FA1A,$88
172 VPOKE 1,$FA1B,$08

200 X = 0
210 Y = 0
220 W = 320
230 H = 240
240 GOSUB 1000

290 FOR Z=0 TO 31
300 H = INT(12*INT(RND(1)*8+1))
310 W = INT(H*(RND(1)+1))
320 X = INT(RND(1)*(319-W))
330 Y = INT(RND(1)*(239-H))
340 GOSUB 1000
399 NEXT

500 GOTO 200

1000 FOR I=0 TO H-1: LINE X,I+Y,X+W-1,I+Y,(I)*(6.0/H)+1: NEXT : REM RAINBOW
1010 ANGLEWIDTH = INT(H / 12)
1015 H2 = H/2
1020 FOR I=0 TO H2-1
1025 LINE X,I+Y,I+X,I+Y,10 : REM WHITE
1026 LINE X,H-1-I+Y,I+X,H-1-I+Y,10
1030 LINE 1+I+X,I+Y,ANGLEWIDTH+I+1+X,I+Y,9 : REM PINK
1040 LINE 1+I+X,H-1-I+Y,ANGLEWIDTH+I+1+X,H-1-I+Y,9
1050 LINE ANGLEWIDTH+I+1+X,I+Y,ANGLEWIDTH*2+I+1+X,I+Y,8 : REM SKY BLUE
1060 LINE ANGLEWIDTH+I+1+X,H-1-I+Y,ANGLEWIDTH*2+I+1+X,H-1-I+Y,8
1070 LINE ANGLEWIDTH*2+I+1+X,I+Y,ANGLEWIDTH*3+I+1+X,I+Y,7 : REM BROWN
1080 LINE ANGLEWIDTH*2+I+1+X,H-1-I+Y,ANGLEWIDTH*3+I+1+X,H-1-I+Y,7
1090 LINE ANGLEWIDTH*3+I+1+X,I+Y,ANGLEWIDTH*4+I+1+X-1,I+Y,0 : REM BLACK
1110 LINE ANGLEWIDTH*3+I+1+X,H-1-I+Y,ANGLEWIDTH*4+I+1+X-1,H-1-I+Y,0
1199 NEXT
1200 FOR I=ANGLEWIDTH TO H2-1
1210 LINE X,I+Y,I-ANGLEWIDTH+X,I+Y,11 : REM INTERYELLOW
1220 LINE X,H-1-I+Y,I-ANGLEWIDTH+X,H-1-I+Y,11
1299 NEXT
1300 CX = INT(H2/2)-ANGLEWIDTH+X+1 : REM +1 IMPROVES SMALLEST SIZE SPACE
1310 CY = H2+Y
1315 R = H/12*0.9 : REM 0.9 IMPROVES ROUNDNESS
1320 X1 = R
1330 Y1 = 0
1335 C = 32 : REM C*2 = CIRCLE SIDES
1340 FOR I=0 TO C-1
1350 THETA = 3.14159265359*(I+1)/C
1360 X2 = COS(THETA)*R
1370 Y2 = SIN(THETA)*R
1380 LINE CX+X1,CY+Y1,CX+X2,CY+Y2,12 : REM INTERPURPLE
1385 LINE CX+X1,CY-Y1,CX+X2,CY-Y2,12
1390 X1 = X2
1395 Y1 = Y2
1399 NEXT
1400 FRAME X,Y,X+W-1,Y+H-1,13 : REM GRAY
1500 RETURN
